apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'net.minecraftforge.gradleutils'

archivesBaseName = 'forgeflower'
 
sourceSets {
    main.java.srcDirs = ['src']
    test.java.srcDirs = ['test']
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(libs.null)

    testImplementation(libs.junit)
    testImplementation(libs.gson)
    testImplementation(libs.hamcrest)
    testImplementation(libs.assertj)
}

// Default all standard Java compile tasks to Java 17
java.toolchain {
    languageVersion = JavaLanguageVersion.of(17)
}

tasks.withType(JavaCompile) {
    options.deprecation = true
}

eclipse {
    project {
        name 'ForgeFlower'
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes('Main-Class': 'org.jetbrains.java.decompiler.main.decompiler.ConsoleDecompiler')
    }
}
java.withSourcesJar()

changelog {
    from('2.0.605')
}
tasks.named('createChangelog').configure {
    gitDirectory = rootProject.layout.projectDirectory.dir('.git')
    outputFile = rootProject.layout.buildDirectory.file('changelog.txt')
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
            name = project.archivesBaseName
            packaging = 'jar'
            description = 'Fernflower from https://github.com/JetBrains/intellij-community as standalone library, With Forges Modifications'
            url = 'https://github.com/MinecraftForge/ForgeFlower'
            
            gradleutils.pom.setGitHubDetails(pom, 'ForgeFlower')
            license gradleutils.pom.licenses.Apache2_0
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

